# Maintainer: Christoph Schmidpeter <john.doe@example.com>
# Co-Maintainer: Mubashshir <ahmubashshir@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# pkg: git

pkgname=anbox-git
pkgver=r1353.31d584e
pkgrel=1
epoch=1
arch=('x86_64')
url="http://anbox.io/"
license=('GPL3')
pkgdesc="Running Android in a container"
depends=('lxc-git' 'sdl2_image' 'protobuf' 'anbox-image' 'boost-libs' 'sdbus-cpp-nosystemd-git')
replaces=('anbox-launchers')
conflicts=('anbox-launchers')
makedepends=('cmake' 'git' 'glm' 'boost' 'properties-cpp' 'gtest' 'python' 'dbus')
source=("git+https://github.com/anbox/anbox.git"
	"git+https://github.com/google/cpu_features.git"
	"https://git.alpinelinux.org/aports/plain/community/anbox/anbox-container-manager.initd"
	"no-bundled-sdbus.patch"
	'anbox-desktop-dir.patch'
	'lxc.patch'
	'99-anbox.rules'
	'anbox.conf'
	'anbox.desktop')
sha256sums=('SKIP'
            'SKIP'
            '22818da270ee7bdfc4074f4f1cecfd2947408914a3c3b249958f41c4580cbfda'
            '7c0626afaf1ce004ac2c57cd89db393f9a6ba21f1087001d15278a7bd79c8219'
            'd1e2e3fa1299b423f1d98cbd3c591d853f1ce8646f508ef838fe714f986fc5b2'
            '9212f8b941d62b50b9188aea6218ceb6f6b77761a388f9868b1071557a95e067'
            '210eb93342228168f7bb632c8b93d9bfda6f53f62459a6b74987fa1e17530475'
            '3e07dc524a827c1651857cce28a06c1565bc5188101c140ed213bbafedc5abff'
            '7332d09865be553a259a53819cebddd21f661c7a251d78c2f46acd75c66676b6')

pkgver() {
  cd "${pkgname%*-git}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${pkgname%*-git}"

  # Don't build tests
  truncate -s 0 cmake/FindGMock.cmake
  truncate -s 0 tests/CMakeLists.txt
  sed -i '1i\#include <cstdint>' "$srcdir/anbox/src/anbox/input/manager.cpp"
  patch -p1 < "$srcdir/anbox-desktop-dir.patch"
  patch -p1 < "$srcdir/lxc.patch"
  patch -p1 < "$srcdir/no-bundled-sdbus.patch"

  git submodule init
  git config submodule.external/cpu_features.url $srcdir/cpu_features
  git submodule update
}

build() {
  cmake -B build -S "${pkgname%*-git}" \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_SKIP_INSTALL_RPATH=YES \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install

  install -Dm 755  "$srcdir/anbox-container-manager.initd" "$pkgdir/etc/init.d/anbox-container-manager"
  install -Dm 755  "$srcdir/anbox/scripts/anbox-bridge.sh" "$pkgdir/usr/share/anbox/anbox-bridge.sh"
  install -Dm 755  "$srcdir/anbox/scripts/anbox-shell.sh" "$pkgdir/usr/share/anbox/anbox-shell.sh"
  install -Dm 644 -t "$pkgdir/usr/lib/udev/rules.d"   99-anbox.rules
  install -Dm 644 -t "$pkgdir/usr/share/applications" anbox.desktop
  install -Dm 644    "${pkgname%*-git}/snap/gui/icon.png"        "$pkgdir/usr/share/pixmaps/anbox.png"
  install -Dm 644 -t "$pkgdir/usr/share/desktop-directories"     "${pkgname%*-git}/data/desktop/anbox-android.directory"
  install -Dm 644 -t "$pkgdir/etc/xdg/menus/applications-merged" "${pkgname%*-git}/data/desktop/anbox-android.menu"
}
