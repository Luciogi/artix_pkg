# Maintainer: Christoph Schmidpeter <john.doe@example.com>
# Co-Maintainer: Mubashshir <ahmubashshir@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# from: git

pkgname=anbox-nosystemd-git
pkgver=r1364.7a0bee7
pkgrel=1
epoch=1
arch=('x86_64')
url="http://anbox.io/"
license=('GPL3')
pkgdesc="Running Android in a container without systemd"
depends=('lxc' 'sdl2_image' 'protobuf' 'anbox-image' 'libcpufeatures' 'boost-libs' 'sdbus-cpp-nosystemd-git')
replaces=('anbox-launchers')
conflicts=('anbox-launchers')
makedepends=('cmake' 'git' 'glm' 'boost' 'properties-cpp' 'gtest' 'python' 'dbus')
source=("git+https://github.com/anbox/anbox.git"
# 	"git+https://github.com/google/cpu_features.git"
# 	'lxc.patch::https://gitlab.alpinelinux.org/alpine/aports/-/raw/fc60972afd4ff0c096498e28557b9a16e801aa7f/community/anbox/lxc4.patch'
	'lxc4.patch::https://gitlab.alpinelinux.org/alpine/aports/-/raw/fc60972afd4ff0c096498e28557b9a16e801aa7f/community/anbox/lxc4.patch'
	'sdbus.patch::https://gitlab.alpinelinux.org/alpine/aports/-/raw/fc60972afd4ff0c096498e28557b9a16e801aa7f/community/anbox/no-bundled-sdbus.patch'
	'delayed-start.patch::https://gitlab.alpinelinux.org/alpine/aports/-/raw/70a1a8afb7b6ac37291d382e6e5cc7561fc88626/community/anbox/give-more-time-to-start.patch'
	#  'webview-opengles3.patch::https://github.com/anbox/anbox/pull/1877.patch'
	'ByteSizeLong.patch::https://github.com/anbox/anbox/pull/1480.patch'
	'desktop-dir.patch'
	'cpu_features.patch'
	#'anbox-container-manager.service'
	#'anbox-session-manager.service'
	'99-anbox.rules'
	'anbox.conf'
	'anbox.desktop'
	#'org.anbox.service'
	#'dev-binderfs.mount'
	#'anbox-bridge.network'
	#'anbox-bridge.netdev'
	)
sha256sums=('SKIP'
#           'SKIP'
            'c1071203defdeaee56122913a6d7e67ac496c889a0c59cb889d94a58bc655bfa'
            '7c0626afaf1ce004ac2c57cd89db393f9a6ba21f1087001d15278a7bd79c8219'
            '253c211455ff0cfaa058a0b80eb41d58f84ee646c0c67dbcbcc888b5833009a7'
            'c922afe6a2cd4fe7296a0921e47e09abedf0104dc23d0037010d1df4cc42308b'
            'd1e2e3fa1299b423f1d98cbd3c591d853f1ce8646f508ef838fe714f986fc5b2'
            'eeeb4dd40d30d0e8c0f0001d44ca7876c1c564dfc31b24ecfb871fd133037825'
            #'be18c27f349560d46f7d7912b3b84d503e477fb643c55e4a7d6de9a84be93d0b'
            #'1f22dbb5a3ca6925bbf62899cd0f0bbaa0b77c879adcdd12ff9d43adfa61b1d8'
            '210eb93342228168f7bb632c8b93d9bfda6f53f62459a6b74987fa1e17530475'
            '3e07dc524a827c1651857cce28a06c1565bc5188101c140ed213bbafedc5abff'
            '7332d09865be553a259a53819cebddd21f661c7a251d78c2f46acd75c66676b6'
            #'74e2208e0c03e5f21be3ff54b58160a172e3768a80c41726c06b4ab8cc9288dd'
            #'c7e9958a9cff1191dca0d50b0ea8392d7728490e56ffb38a475ba516a09a2109'
            #'44899328725667041e6e84912da81c1d0147b708006eb2c2bb6503f271629ff0'
            #'559190df4d6d595480b30d8b13b862081fc4aac52790e33eb24cf7fbcb8003b8'
            )

pkgver() {
   cd "${pkgname%*-nosystemd-git}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${pkgname%*-nosystemd-git}"
  
  # Don't build tests
  truncate -s 0 cmake/FindGMock.cmake
  truncate -s 0 tests/CMakeLists.txt
  
  # Thanks to @bartus <arch-user-repo@bartus.33mail.com> for suggesting the patch
  # checking before applying it.
  # Original patch: http://ix.io/3Clr
  printf '%s\n' "${source[@]%%::*}" \
    | awk -F . '/\.patch/{print $1}' \
    | while read -r patch;do
        if patch --dry-run -Np1 -Rsf < "$srcdir/$patch.patch" &> /dev/null; then
          warning "Patch already applied, please remove $patch.patch from source[]!"
          read -t5 -p 'press enter to continue...' || true
        else
          patch -Np1 < "$srcdir/$patch.patch"
        fi
      done
}

build() {
  cmake -B build -S "${pkgname%*-nosystemd-git}" \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_SKIP_INSTALL_RPATH=YES \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install

#   install -Dm 644 -t "$pkgdir/usr/lib/systemd/system"     anbox-container-manager.service dev-binderfs.mount
#   install -Dm 644 -t "$pkgdir/usr/lib/systemd/user"       anbox-session-manager.service
#   install -Dm 644    "$srcdir/anbox-bridge.network"       "$pkgdir/usr/lib/systemd/network/80-anbox-bridge.network"
#   install -Dm 644    "$srcdir/anbox-bridge.netdev"        "$pkgdir/usr/lib/systemd/network/80-anbox-bridge.netdev"
  install -Dm 644 -t "$pkgdir/usr/lib/udev/rules.d" 99-anbox.rules
  install -Dm 644 -t "$pkgdir/usr/share/applications"   anbox.desktop
#   install -Dm 644 -t "$pkgdir/usr/share/dbus-1/services"  org.anbox.service
  install -Dm 644    "${pkgname%*-nosystemd-git}/snap/gui/icon.png" "$pkgdir/usr/share/pixmaps/anbox.png"
  install -Dm 644 -t "$pkgdir/usr/share/desktop-directories"    "${pkgname%*-nosystemd-git}/data/desktop/anbox-android.directory"
  install -Dm 644 -t "$pkgdir/etc/xdg/menus/applications-merged"    "${pkgname%*-nosystemd-git}/data/desktop/anbox-android.menu"

  install -Dm 755  "$srcdir/anbox/scripts/anbox-bridge.sh"  "$pkgdir/usr/share/anbox/anbox-bridge.sh"
  install -Dm 755  "$srcdir/anbox/scripts/anbox-shell.sh"   "$pkgdir/usr/share/anbox/anbox-shell.sh"
}

